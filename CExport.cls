VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
  Persistable = 0  'NotPersistable
  DataBindingBehavior = 0  'vbNone
  DataSourceBehavior  = 0  'vbNone
  MTSTransactionMode  = 0  'NotAnMTSObject
END
Attribute VB_Name = "CExport"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

Private Const k_COLUMN_ONE = 21
Private Const k_COLUMN_TWO = 29
Private Const k_COLUMN_COMMENT = 81

Private m_exportData As String

Public Function GetData() As String
    GetData = m_exportData
End Function

Public Sub Reset()
    m_exportData = ""
End Sub

Public Sub CRLF(Optional ByVal num As Integer)
    Dim index As Integer
    
    Debug.Assert num >= 0 And num <= 64
    If num = 0 Then
        m_exportData = m_exportData & vbCrLf
    Else
        For index = 1 To num
            m_exportData = m_exportData & vbCrLf
        Next
    End If
End Sub

Public Sub Text(ByVal s As String)
    m_exportData = m_exportData & s
End Sub

Public Sub TextRepeat(ByVal s As String, ByVal repeatCount As Integer)
    Debug.Assert repeatCount >= 2 And repeatCount <= 128
    m_exportData = m_exportData & String$(repeatCount, s)
End Sub

Public Sub Pad(ByVal column As Integer)
    Dim index As Long
    Dim pos As Long
    Dim stringLen As Long
    
    stringLen = Len(m_exportData)
    index = InStrRev(m_exportData, vbCrLf)
    If index = 0 Then
        pos = stringLen
    Else
        pos = stringLen - index
    End If
    
    If pos >= column Then
        Exit Sub
    End If
    
    m_exportData = m_exportData & Space$(column - pos)
End Sub

Public Sub EQU(ByVal label As String, ByVal value As String)
    Text label
    Pad 32
    Text " EQU"
    Pad 41
    Text value
    CRLF
End Sub

Public Sub Comment(Optional ByVal s As String)
    m_exportData = m_exportData & "; " & s
    CRLF
End Sub
    
Public Sub Header(ByVal outputFilename As String, ByVal title As String, ByVal levelFilename As String)
    Dim filename As New CFilepath
    
    ' start comment block
    Text ";"
    TextRepeat "*", k_COLUMN_COMMENT - 1
    CRLF
    
    ' blank line
    Text ";*"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' name comment
'    filename = RemovePathname(outputFilename)
    filename.filepath = outputFilename
    Text ";* NAME: """ & filename.Filespec & """"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' source file comment
'    filename = RemovePathname(levelFilename)
    filename.filepath = levelFilename
    Text ";* SRC:  """ & filename.Filespec & """"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' date comment
    Text ";* DATE:  " & Date
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' blank line
    Text ";*"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' title comment
    Text ";* " & title
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' blank line
    Text ";*"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' copyright line
    Text ";* (c) Copyright 1999 Justin Lloyd, all rights reserved."
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' copyright line
    Text ";* This file may not be copied, in whole, or in part,"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' copyright line
    Text ";* by any means without prior written consent of the author."
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF

    ' blank line
    Text ";*"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' do not edit warning
    Text ";* This file is automatically generated, do not edit"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF

    ' blank line
    Text ";*"
    Pad k_COLUMN_COMMENT
    Text "*"
    CRLF
    
    ' end comment block
    Text ";"
    TextRepeat "*", k_COLUMN_COMMENT - 1
    CRLF
    CRLF 2
End Sub

Public Sub ConditionalInclude(ByVal includeName As String)
    Dim conditionalname As String
    
    conditionalname = "__" & Trim(UCase(includeName)) & "_I__"
    Pad k_COLUMN_ONE
    Text "IF"
    Pad k_COLUMN_TWO
    Text "!DEF(" & conditionalname & ")"
    CRLF
    Text conditionalname
    Pad k_COLUMN_ONE
    Text "SET"
    Pad k_COLUMN_TWO
    Text "1"
    CRLF
End Sub

Public Sub ConditionalDefine(ByVal flagName As String)
    Dim conditionalname As String
    
    conditionalname = Trim(UCase(flagName))
    Pad k_COLUMN_ONE
    Text "IF"
    Pad k_COLUMN_TWO
    Text "!DEF(" & conditionalname & ")"
    CRLF
    Text conditionalname
    Pad k_COLUMN_ONE
    Text "SET"
    Pad k_COLUMN_TWO
    Text "1"
    CRLF
End Sub

Public Sub ConditionalEnd()
    Pad k_COLUMN_ONE
    Text "ENDC"
    CRLF
End Sub

Public Sub DB(ByVal bytes As String, Optional ByVal commentText As String)
    Pad k_COLUMN_ONE
    Text "DB"
    Pad k_COLUMN_TWO
    Text bytes
    If Not IsMissing(commentText) Then
        Pad k_COLUMN_COMMENT
        Text "; " & commentText
    End If
    CRLF
End Sub

Public Sub DW(ByVal words As String, Optional ByVal commentText As String)
    Pad k_COLUMN_ONE
    Text "DW"
    Pad k_COLUMN_TWO
    Text words
    If Not IsMissing(commentText) Then
        Pad k_COLUMN_COMMENT
        Text "; " & commentText
    End If
    CRLF
End Sub

Public Sub RSRESET()
    Pad k_COLUMN_ONE
    Text "RSRESET"
End Sub

Public Sub RSSET(ByVal startValue As Integer)
    Pad k_COLUMN_ONE
    Text "RSSET " & Trim(startValue)
End Sub

Public Sub RB(ByVal label As String, Optional ByVal size As Integer, Optional ByVal commentText As String)
    Debug.Assert size >= 0 And size <= 1024
    Debug.Assert Len(label) > 0
    Text label
    Pad k_COLUMN_ONE
    Text "RB"
    Pad k_COLUMN_TWO
    Text size
    If Not IsMissing(commentText) Then
        Pad k_COLUMN_COMMENT
        Text "; " & commentText
    End If
    CRLF
End Sub

Public Sub DS(ByVal label As String, Optional ByVal size As Integer, Optional ByVal commentText As String)
    Debug.Assert size >= 0 And size <= 1024
    Debug.Assert Len(label) > 0
    Text label
    Pad k_COLUMN_ONE
    Text "DS"
    Pad k_COLUMN_TWO
    Text size
    If Not IsMissing(commentText) Then
        Pad k_COLUMN_COMMENT
        Text "; " & commentText
    End If
    CRLF
End Sub

Public Sub DeclareGlobal(ByVal label As String)
    Pad k_COLUMN_ONE
    Text "GLOBAL"
    Pad k_COLUMN_TWO
    Text label
    CRLF
End Sub

Private Sub Class_Initialize()
    Reset
End Sub


